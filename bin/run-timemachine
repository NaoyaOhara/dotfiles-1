#!/usr/bin/env perl
use 5.14.0;
use warnings;
use File::Basename;
use File::Temp;
use JSON::PP;
use Time::Piece;

sub with_timestamp($) {
    (my $str = shift) =~ s/^/(localtime->datetime) . "\t"/egm;
    return $str;
}

sub logger($) {
    say with_timestamp shift;
}

sub safe_system($) {
    my $cmd = shift;

    my $stderr_file = File::Temp->new->filename;
    chomp(my $stdout = `$cmd 2> $stderr_file`);
    my $stderr = do {
        open my $fh, '<', $stderr_file or die;
        local $/;
        <$fh>;
    };
    die with_timestamp "failed to execute: $cmd\n$stderr" if $? != 0 || $stderr;
    return $stdout;
}

my $json = safe_system "$ENV{HOME}/git/dotfiles/bin/tmstatus";
my $status = do { decode_json($json) };
if ($@) {
    die with_timestamp $@;
} elsif ($status->{running} || $status->{stopping}) {
    # Starting
    # MountingBackupVol
    # ThinningPreBackup
    # Copying
    # LazyThinning
    logger 'Timemachine is running: ' . $status->{backupPhase};
    logger sprintf '%s / %s EST %s',
        $status->{progress}{bytesFormatted},
        $status->{progress}{totalBytesFormatted},
        $status->{progress}{timeRemainingFormatted};
    exit;
} elsif ($status->{backupPhase}) {
    logger 'status => ' . $status->{backupPhase};
} else {
    logger "status: $json";
    exit;
}

my $latest_backup = safe_system '/usr/bin/tmutil latestbackup';
my $t = Time::Piece->strptime(basename($latest_backup), '%F-%H%M%S');
logger 'The latest backup: ' . $t->datetime;
if ((time - $t->epoch) > 12 * 60 * 60) {
    logger 'starting backup...';
    safe_system '/usr/bin/tmutil startbackup --auto';
    logger 'finished!';
} else {
    logger 'need not to backup';
}
