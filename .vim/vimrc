scriptencoding 'utf-8'

if &compatible
  " vint: -ProhibitSetNoCompatible
  set nocompatible               " Be iMproved
  " vint: +ProhibitSetNoCompatible
endif

let g:is_mac = has('mac')
let g:is_win = has('win32') || has('win64')

if len($H)
  let g:home = expand('$H')
  set runtimepath-=~/.vim
  set runtimepath+=$H/.vim
elseif len($USERPROFILE)
  let g:home = expand('$USERPROFILE')
else
  let g:home = expand('~')
endif

let g:vim_home = g:home . (g:is_win ? '/vimfiles' : '/.vim')
let g:rc_dir = g:vim_home . '/rc'
let g:after_dir = g:home . '/.vim/after'
execute 'set runtimepath+=' . g:after_dir . '/'

augroup MyAutoCmd
  autocmd!
augroup END

function s:load_rc(file)
  execute 'source ' . g:rc_dir . '/' . a:file . '.vim'
endfunction

function s:load_bundle_rc(bundle_name)
  if dein#tap(a:bundle_name)
    execute 'autocmd MyAutoCmd User' 'dein#source#'.g:dein#name
          \ 'call s:load_rc("' . g:dein#name . '")'
  endif
endfunction

call s:load_rc('dein')                " プラグインの読み込み
call s:load_rc('map')                 " マッピング
call s:load_rc('set')                 " オプション設定
call s:load_rc('term')                " ターミナル固有の設定
call s:load_rc('commands')            " ユーティリティコマンド
call s:load_rc('filetypes')           " ファイルタイプごとの設定

call s:load_rc('title')               " screen title 設定

" 遅延ロード {{{
call s:load_bundle_rc('neocomplete.vim') " neocomplete 設定
"}}}

" オフィス専用設定
let g:office_vimrc = g:home . '/git/dotfiles-office/.vimrc'
if filereadable(g:office_vimrc)
  execute 'source ' . g:office_vimrc
endif

" ローカル設定
let g:local_vimrc = g:home . '/.vimrc-local'
if filereadable(g:local_vimrc)
  execute 'source ' . g:local_vimrc
endif
